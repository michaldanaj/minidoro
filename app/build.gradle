apply plugin: 'com.android.application'

android {
	compileSdkVersion 34
	namespace 'com.github.michaldanaj.minidoro'

	defaultConfig {
		applicationId "com.github.michaldanaj.minidoro"
		resValue "string", "packageName", applicationId
		minSdkVersion 14
		targetSdkVersion 33
		versionCode 114
		versionName "1.14"
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	buildTypes {
		debug {
			minifyEnabled false

		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:deprecation'
	}
}

dependencies {
	testImplementation 'junit:junit:4.12'
	testImplementation 'org.mockito:mockito-core:4.1.0'
}

android.applicationVariants.all { variant ->
	if ((variant.name).equals("release")) {
		def task = project.tasks.create('createChromeExtension', Copy)
		def apk = null
		variant.outputs.each {
			output -> apk = output.outputFile
		}
		task.from(apk) {
			rename { String fileName -> 'Minidoro.apk' }
			into "vendor/chromium/crx"
		}
		task.from("src/main/res/mipmap/minidoro.png") {
			into "."
		}
		task.into "crx"

		task.dependsOn variant.assemble
	}
}
